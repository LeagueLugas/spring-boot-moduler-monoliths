buildscript {
    ext {
        dependencyManagementVersion = '1.0.10.RELEASE'
        springBootVersion = '2.2.1.RELEASE'
    }

    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }

    dependencies {
        classpath("io.spring.gradle:dependency-management-plugin:${dependencyManagementVersion}")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    configurations {
        developmentOnly
        runtimeClasspath {
            extendsFrom developmentOnly
        }
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }
        dependencies {

        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        implementation 'org.slf4j:slf4j-api'
    }

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    compileJava.options.encoding = 'UTF-8'

    test {
        useJUnitPlatform()
    }
}

project(':common') {
    dependencies {
        compile 'org.springframework:spring-context'
        compile 'org.springframework:spring-context-support'
    }
}

project(':auth') {
    dependencies {
        compile project(':common')

        compile 'org.springframework.boot:spring-boot-starter-web'

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
        testImplementation 'org.springframework.security:spring-security-test'
    }
}

project(':order') {
    dependencies {
        compile project(':common')

        compile 'org.springframework.boot:spring-boot-starter-web'

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
        testImplementation 'org.springframework.security:spring-security-test'
    }
}

project(':modular-monoliths') {
    apply plugin: 'org.springframework.boot'

    dependencies {
        compile project(':auth')
        compile project(':order')

        implementation 'org.springframework.boot:spring-boot-starter-web'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }

        developmentOnly 'org.springframework.boot:spring-boot-devtools'
    }
}
